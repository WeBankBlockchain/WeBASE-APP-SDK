group 'com.webank'
version = '1.5.5'

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

repositories {
    maven {url "http://maven.aliyun.com/nexus/content/groups/public/" }
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.bouncycastle:bcprov-jdk15on:1.67'
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'com.squareup.okhttp3:okhttp:4.8.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.14.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.2'
    implementation 'com.fasterxml.jackson.module:jackson-module-parameter-names:2.14.2'
    implementation 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.20.0'
    implementation 'org.slf4j:slf4j-api:1.7.36'
}

clean {
    delete 'dist'
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

jar.dependsOn(sourcesJar)

jar {
    destinationDir file("dist/v${project.version}")
    archiveName project.name + "-" + "${project.version}" + ".jar"

    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version)
    }
    exclude '**/*.xml'
    
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (project.version.endsWith('-SNAPSHOT')) {
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            } else {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }

            pom.project {
                name project.name
                packaging 'jar'
                description 'WeBASE-APP-SDK for application integration'
                url 'https://github.com/WeBankBlockchain/WeBASE-APP-SDK'

                scm {
                    url 'scm:git@github.com:WeBankBlockchain/WeBASE-APP-SDK.git'
                    connection 'scm:git@github.com:WeBankBlockchain/WeBASE-APP-SDK.git'
                    developerConnection 'git@github.com:WeBankBlockchain/WeBASE-APP-SDK.git'
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'tonychen'
                        name 'tonychen'
                    }
                }
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


//publishing {
//    publications {
//        mavenJava(MavenPublication) {
//            from components.java
//            artifact sourcesJar
//            artifact javadocJar
//        }
//    }
//    repositories {
//        maven {
//            if (project.version.endsWith('-SNAPSHOT')) {
//                url = 'https://oss.sonatype.org/content/repositories/snapshots/'
//            } else {
//                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
//            }
//            credentials {
//                username = ossrhUsername
//                password = ossrhPassword
//            }
//
//
//        }
//    }
//}
//
//signing {
//    sign configurations.archives
//}
